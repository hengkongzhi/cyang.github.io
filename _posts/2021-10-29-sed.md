## sed
### 常用的正则表达式
+ 字符匹配  
    + **`.`**：匹配任意单个字符  
    + **`[]`**：匹配指定范围内任意单个字符，read/rebd可由re[ab]d匹配  
    + **`[^]`**：匹配指定范围外的任意单个字符  
    + **`[[:digit:]]`**：匹配任意单个数字，等价于[0-9]  
    + **`[[:lower:]]`**：匹配任意单个小写字母  
    + **`[[:upper:]]`**：匹配任意单个大写字母  
    + **`[[:space:]]`**：匹配单个空格  
    + **`[[:alpha:]]`**：匹配任意单个英文字母  
    + **`[[:alnum:]]`**：匹配任意单个字母或单个数字  
+ 匹配次数  
    + **`*`**：匹配前面的字符任意次(>=0)  
    + **`.*`**：任意长度任意字符(可以为空)  
    + **`\?`**：匹配前面的字符0次或1次  
    + **`\+`**：匹配前面的字符至少1次  
    + **`\{m\}`**：匹配前面的字符m次  
    + **`\{m,n\}`**：匹配前面的字符至少m次，至多n次([m,n])  
    + **`\{m,\}`**：匹配前面的字符至少m次  
+ 位置锚定  
    + **`^`**：行首锚定  
    + **`$`**：行尾锚定  
    + **`^PATTERN$`**：用于匹配整行  
    + **`^$`**：空行  

### sed说明及常用操作
+ sed中的分隔符可以是任意字符，**`/`**、**`|`**、**`#`**、**`:`**等  
+ sed匹配模型，一般用**`'`**或者**`"`**括起来  
+ sed中**`'s///g'`**以**`s`**开头表示替换，以**`g`**结尾表示为全局替换  
+ sed中**`'s///p'`**以**`s`**开头表示替换，以**`p`**结尾表示为打印，一般需加**`-n`**命令行选项，只显示替换后的内容，但是会将替换后的内容所在的整行进行打印，以**`pg`**结尾表示全局替换并打印，以**`w file`**结尾表示将替换后的内容所在的整行写入file文件中，以**`I`**结尾表示匹配内容不区分大小写  
+ **`sed 's/[ab]/&/'`**，其中**`&`**表示匹配到的结果，可以是a或b  
+ sed使用**`\(\)`**和**`\1`**来获取匹配到的部分，**`\2`**表示匹配到的第二个括号部分，以此类推，最多可以有9个括号；**`\1`**也可以在替换的前半部分位置  
+ sed中**`s/abc//2`**，表示对某行中的第二个abc进行删除，**`s/abc//2g`**，表示除第一个abc不删除外，其余均删除；**`s/./&:/80`**表示对一行中的第80个字符之后加**`:`**号，sed中最后的数字可以为1到512之间的任意值  
+ sed中可以一次操作几个文件，**`sed 's/^abc.*//g' 1.txt 2.txt | grep -v '^$' | wc -l`**表示将1.txt和2.txt中，以abc开头的行删空，并统计不是以abc开头的行数  
+ sed中**`-e`**命令行选项，可以一次性执行多条命令，例如：**`sed -e 's/a/A/' -e 's/B/b/' 1.txt`**，表示一次性将a替换为A和将B替换为b  
+ sed中**`-f`**命令行选项，可以接由多个sed命令组成的脚本文件，例如**`sed -f 1.sed 1.txt`**，表示对1.txt执行1.sed脚本中的内容，1.sed脚本不需要**`'`**号，且每条命令独占一行  
+ sed后如果加了**`-r`**命令行选项，**`+`**，**`?`**，**`{m}`**，**`(PATTERN)`**不用加反斜杠就能分别表示：前一字符至少1次，前一字符0次或1次，前一字符m次，获取匹配到的PATTERN部分  
+ sed中**`-i`**命令行选项，表示直接修改所操作的文件内容，例如**`sed -i s/abc/123/g 1.txt`**，表示直接将文件中abc全部替换为123  
+ sed命令也可以写成一个可执行脚本(2sed)，如下所示：
```
#！/bin/sed -f
s/abc/123/g
s/AAZ/aad/g
```
对1.txt文件执行sed替换操作：**`./2sed 1.txt`**  

### 对匹配范围的操作
+ sed命令只对具体某一行进行操作，例如**`sed '3 s/[a-z][a-z]*//g' 1.txt`**，表示只对第3行进行替换删空操作  
+ sed命令对行号范围的操作，例如**`sed '1,3 s/[a-z][a-z]*//g' 1.txt`**，表示对第1至3行进行替换删空操作，用**`1,$`**表示第1至最后一行  
+ sed中也有模式匹配，例如**`sed '/^#/ s/[0-9][0-9]*//' 1.txt`**，表示匹配以#开头的行，并将第一个数字删空；如果查找一个以**`/`**开头的行，则需要使用反斜杠转义或者其他分隔符进行模式匹配，例如**`sed '\_/_ s/[0-9][0-9]*//' 1.txt`**，这里使用下划线进行匹配，但是需要在下划线前加**`\`**  
+ sed中的匹配模式范围，以匹配到的第一个模式为开始，以匹配到的第二个模式为结束，注意匹配将会是贪心的，例如**`sed '/^#/,/asd/ s/[0-9][0-9]*//' 1.txt`**，表示以#开头的行为起始行，以匹配到asd为结束行，对包括起始行到结束行的第一个数字删空；也可以是数字和模式匹配共同决定操作范围，例如**`sed '1,/asd/ s/[0-9][0-9]*//' 1.txt`**，表示第一行到匹配到的asd所在行，对行中的第一个数字删空  
+ sed中进行删除，例如**`sed '2,$ d' 1.txt`**，表示删除1.txt中的第二行到最后一行，匹配模式，匹配模式范围，行号，行号范围，依然适用于用**`d`**进行删除  
+ sed中进行打印，例如**`sed '2,$ p' 1.txt`**，表示除打印第一行外，重复打印1.txt中的第二行到最后一行，匹配模式，匹配模式范围，行号，行号范围，依然适用于用**`p`**进行打印，加**`-n`**命令行选项，只显示匹配的内容，会将匹配到的内容所在的整行进行打印  
+ sed中针对**`p`**和**`d`**前可以加**`!`**，表示操作未匹配到的内容  

### 更多操作指令
+ sed中使用**`a`**在匹配到内容的后一行增加内容，例如在匹配到123的下一行增加内容：**`sed '/123/a we are family' 1.txt`**(要写入的内容首字符为空格，可以使用**`“\ ”`**的方式解决；要写入多行，shell脚本中用**`\`**解决，sed命令行中用**`\n`**解决)  
+ sed中使用**`c`**将匹配到内容的所在行内容全部替换，例如在匹配到123的所在行内容替换：**`sed '/123/c we are family'`**(要写入的内容首字符为空格，可以使用**`“\ ”`**的方式解决；要写入多行，shell脚本中用**`\`**解决，sed命令行中用**`\n`**解决)  
+ sed中的匹配模式，匹配模式范围，行号，行号范围，依然适用于用**`a`**、**`i`**、**`c`**命令，注意如果是范围匹配使用**`c`**命令，则将是把匹配范围的所有内容都修改，例如**`sed '1,$ c we are family\nwe are together' 1.txt`**  
+ sed中使用**`q`**来从某行退出，**`sed '10 q' 1.txt`**类似于shell命令**`head -10 1.txt`**  
+ sed中使用**`r`**来读文件，例如将读入的文件内容写在匹配到123的下一行：**`sed '/123/r file' 1.txt`**  
+ sed中使用**`=`**可以计算匹配到的内容所在的行数，例如**`sed -n '/123/=' 1.txt`**；也可计算出一个文件的总行数，例如**`sed -n '$=' 1.txt`**  

### shell中的使用
+ shell脚本中，sed使用shell变量，需要加单引号，例如**`var="hello"; sed -i 's/'$var'/abc/ 1.txt`**，但如果var变量中有空格字符，则sed命令不能使用该变量  
+ sed在shell脚本中匹配模式范围且执行多条语句，如下所示：  
```sh
#!/bin/sh
#满足匹配#1234到dsa之间的范围（包括#1234和dsa所在的行）
sed -n '/^#1234/,/dsa/ {
    s/#.*//    #在满足匹配模式的范围内，清除以#开头的行
    /^$/d      #在满足匹配模式的范围内，清除空行
    p          #在满足匹配模式的范围内，打印
}' 1.txt
#第1到4行之间，满足匹配#1234到dsa之间的范围
sed -n '1,4 {
    /^#1234/,/dsa/ {
        s/#.*//    #在满足匹配模式的范围内，清除以#开头的行
        /^$/d      #在满足匹配模式的范围内，清除空行
        p          #在满足匹配模式的范围内，打印
    }
}' 1.txt
#在大括号前加!,表示对不在匹配#1234到dsa之间的范围执行命令
sed -n '/^#1234/,/dsa/ !{
    s/#.*//    #在不满足匹配模式的范围内，清除以#开头的行
    /^$/d      #在不满足匹配模式的范围内，清除空行
    p          #在不满足匹配模式的范围内，打印
}' 1.txt
#在大括号中加匹配模式和n，表示遇到匹配模式跳过
sed '/^abc/,/won/ {
    /dsa/n        #在满足匹配模式的范围内，匹配到dsa，跳过
    s/123/456/    #在满足匹配模式的范围内，将123替换为456
}' 1.txt
```